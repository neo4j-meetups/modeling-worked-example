= Similar groups
:csv-url: https://raw.githubusercontent.com/neo4j-meetups/modeling-worked-example/master/data/
:icons: font

== Find similar groups to Neo4j

image::{img}/group_has_topic.png[]

[verse]
____
As a member of the Neo4j London group
I want to find other similar meetup groups
So that I can join those groups
____

One thing that makes groups similar is the topics that they’re about.
We're going to start the session by importing meetup groups and their topics into Neo4j.

== What makes groups similar?

We've done the pre processing work of taking JSON from meetup.com's APIs and converting it into CSV files to make it easy to import into Neo4j.

Let's start by exploring the groups CSV file to see what data we have available to us:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}groups.csv"
AS row
RETURN row
LIMIT 10
----

We've got a group id, name, description and a few other attributes as well.
Now we'll convert those rows in the CSV file into entities in the graph.

== Import Groups

Run the following query to create a node for each group in the CSV file.

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}groups.csv"
AS row
MERGE (group:Group { id:row.id })
ON CREATE SET
  group.name = row.name,
  group.urlname = row.urlname,
  group.rating = toInt(row.rating),
  group.created = toInt(row.created)
----

The `MERGE` command will create a node with the `Group` label with and set an `id` property using the value in the CSV file.
If there's already a node in the database with that value then it won't create anything.

We've also set several other several properties.
By default properties are stored as strings.
We need to do an explicit conversion (e.g. `toInt`) if we know that the data in our CSV file has a different type.

Let's run a quick query to check what we've actually imported:

[source,cypher]
----
MATCH (g:Group)
RETURN g.id, g.name, g.urlname
----

_Hint_ You can type `Cmd + Enter` or `Control + Enter` to execute a query rather than always pressing the play button.
More shortcuts are available.
See `:help keys`

== Import Topics

Now we're going to import topics.
Let's first check what we've got in our CSV file:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}groups_topics.csv"  AS row
RETURN row
LIMIT 10
----

So we've got a topic id, name and urlkey as well as a group id.
We'll ignore the group id for now and run the following query to import topics into the graph:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}groups_topics.csv"  AS row
MERGE (topic:Topic {id: row.id})
ON CREATE SET topic.name = row.name, topic.urlkey = row.urlkey
----

Let's quickly check that they got imported correctly:

[source,cypher]
----
MATCH (t:Topic)
RETURN t.id, t.name
----

At this point we've got topics and groups floating around but nothing connecting them together!
In the next section we'll add relationships between them.

== Connect groups and topics

You'll have noticed that the CSV file we used to import topics also contained a `group_id` field for each row.
We're now going to process this file again but this time we'll find a topic and group for each row and then create a relationship between them.

Before we do that we're going to create some indexes to help speed up the import of our data:

image::{img}/slides.jpg[]

== Create group and topic indexes

Run the following queries to create indexes on `:Topic(id)` and `:Group(id)`:

[source,cypher]
----
CREATE INDEX ON :Group(id)
----

[source,cypher]
----
CREATE INDEX ON :Topic(id)
----

Run the following command to check that those indexes were created:

[source,cypher]
----
:schema
----

Now we're ready to connect groups and topics.

== Connect groups and topics

Run the following query to create a `HAS_TOPIC` relationship between groups and topics:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}groups_topics.csv"  AS row
MATCH (topic:Topic {id: row.id})
MATCH (group:Group {id: row.groupId})
MERGE (group)-[:HAS_TOPIC]->(topic)
----

The use of `MERGE` here ensures that we end up with a single unique link between a group and a topic.

If we run the query a second time nothing will happen.
If we run another `MERGE` query that tries to create a `HAS_TOPIC` relationship when one already exists it won’t do anything.

Let's now check that the data was imported correctly:

[source,cypher]
----
MATCH (group:Group)-[:HAS_TOPIC]->(topic:Topic)
RETURN group, topic
LIMIT 10
----

Try double clicking on the nodes to expand other nodes that are connected to them.
You can also single click to view a context menu with other options.

== More indexes

We'll probably want to search for groups and topics by name so let's define indexes to allow us to do that quickly.

[source,cypher]
----
CREATE INDEX ON :Group(name)
----

Let's do the same for topics as well:

[source,cypher]
----
CREATE INDEX ON :Topic(name)
----

We can see which indexes we have by using the `:schema` command or by running the following command:

[source,cypher]
----
CALL db.indexes()
----

ifdef::env-training[]

== Exercise: Explore the graph

We've now loaded groups and topics but we don't know exactly what's in our graph so let's do some exploration.

* What's the most popular topic?
* Which group was created most recently?
* How many groups have been running for at least 4 years?

_Hint:_ The link:http://neo4j.com/docs/milestone/cypher-refcard/[Cypher refcard] will come in handy for syntax we haven't covered yet!

== Answer: What's the most popular topic?

[source,cypher,subs=attributes]
----
MATCH (t:Topic)<-[:HAS_TOPIC]-()
RETURN t.name, COUNT(*) AS count
ORDER BY count DESC
----

== Answer: Which group was created most recently?

[source,cypher,subs=attributes]
----
MATCH (g:Group)
RETURN g
ORDER BY g.created DESC
LIMIT 1
----

== Answer: How many groups have been running for at least 4 years?

[source,cypher,subs=attributes]
----
WITH (4 * 365 * 24 * 60 * 60 * 1000) AS fourYears
MATCH (g:Group)
WHERE g.created < timestamp() - fourYears
RETURN g
----
endif::env-training[]

==  Find similar groups to Neo4j

So you've hopefully now got an idea of what the data looks like.
It's time to write our first recommendation query which will find groups that have the same topics as the Neo4j London group:

[source,cypher]
----
MATCH (group:Group {name: "Neo4j - London User Group"})-[:HAS_TOPIC]->(topic)<-[:HAS_TOPIC]-(otherGroup)
RETURN otherGroup.name, COUNT(topic) AS topicsInCommon,
       COLLECT(topic.name) AS topics
ORDER BY topicsInCommon DESC, otherGroup.name
LIMIT 10
----

This query

* starts from the Neo4j group,
* finds its topics,
* then looks for other groups that have those topics
* and aggregates the groups with the most topics in common.

Try changing the group name e.g. `Big Data Debate` or `Docker London` and see how the results change.

== Next Step

In the next section we're going to introduce members into the graph.
We'll then look at our graphs and write some queries to recommend new groups for ourselves.

pass:a[<a play-topic='{guides}/02_my_similar_groups.html'>Groups similar to mine</a>]
